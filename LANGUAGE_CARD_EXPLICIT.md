# Oxide Explicit — Language Card (v0.2.1)

Oxide Explicit is a Rust-like language with **explicit linear types**: each
value is consumed exactly once, and you write the ownership out — `&` marks
every read of a linear value, parameter types mark read vs consume, and
`drop` statements destroy values exactly where their lives end. The checker
verifies every annotation. Types are fully inferred; annotations optional.

## Syntax essentials

- No semicolons — newlines end statements. Blocks `{ }` are expressions; the
  last expression in a block is its value.
- Items: `fn name(param: Type, ...) -> Type { ... }`, `struct Name { field: Type }`,
  `enum Name { Variant(Type, ...), Nullary }`.
- Statements: `let x = expr` · `x = expr` (reassignment) · `return expr` ·
  `while cond { }` · `for x in vec_expr { }` · `break` · `continue` ·
  `drop name` · expression statements.
- Compound assignment: `x += e` / `x -= e` / `x *= e` desugar to
  `x = x + e` / `x = x - e` / `x = x * e`, identifier targets only.
  Int/Float only — `+` isn't defined for Str (concat is `concat(a, b)`),
  so `s += t` errors like `s = s + t`.
- `if c { a } else { b }` and `match e { Pat => expr, _ => expr }` are
  expressions; match arms are `Variant(binders)`, `Nullary`, or `_`.
- `expr?` unwraps an `Option`/`Result`, returning the `None`/`Err` early; the
  function must return the same wrapper kind.
- Functional update: `Point { x: 5, ..p }` takes every unlisted field from
  `p` (consumes `p`; `..p` last).
- `&name` marks a read of a linear variable; `name: &Type` a read-only
  parameter.
- `let mut x = e` means `let x = e`; bindings are already reassignable.
- Builtins accept receiver-first method syntax, and the receiver carries its
  own ownership marker exactly as it would in prefix form: a read is
  `(&v).len()` — the same `&` you would write in `len(&v)` — and a consuming
  call is `v.push(x)`, matching `push(v, x)`. Writing `v.len()` is an error
  (`EX0002`): the read marker is still required. Only builtins; there are no
  user-defined methods, so `p.area()` is an error.
- Variant names are global — write `Circle(1.5)`, not `Shape::Circle(1.5)`.
- Comparison chains like `a < b < c` are not allowed; parenthesize.

## Types

`Int`, `Float`, `Bool`, `Str`, `Unit`, `Vec<T>`, user structs and enums,
plus builtin `Option<T>` (`Some(x)` / `None`) and `Result<T, E>`
(`Ok(x)` / `Err(e)`). Int and Float never mix implicitly — convert with
`to_float` / `trunc`. Structs/enums are declared non-generic.

## Ownership (the part you must write yourself)

`Int`, `Float`, `Bool`, `Unit` are copied freely: always bare, never `&x`,
never dropped. Everything else (`Str`, `Vec`, structs, enums) is **linear** —
every use of a linear variable is a move or a read, and you must write the
matching form:

**Moves — write `name` bare.** A use consumes when it: initializes a `let`
(`let y = x`); is an argument to a consuming parameter (`push`, `concat`, any
param declared without `&`); is returned; is placed in a struct literal or
variant; is the `..rest` of a functional update; is destructured or matched;
or has `?` applied.

**Reads — write `&name`.** A use only reads when it: is an argument to a
read-only parameter (`print(&x)`, `len(&v)`, any `&Type` param); is
an operator operand (`&a == &b` for linear values — numbers and bools stay
bare); is iterated (`for x in &v` — each `x` is a fresh copy); or is the base
of a field access (`&s.f` — the field arrives as a fresh copy).

**Parameter modes.** Declare `name: &Type` when the function only reads it,
bare `name: Type` when any path consumes it; copy params always bare. A
`&Type` param is read-only — every body use is `&name` — and belongs
to the caller: never drop it.

**Drop placement.** Write `drop name` exactly where a value's life ends:
- Last use is a read → `drop name` immediately after that statement.
- Never used → `drop name` at the end of its defining block.
- One branch consumes, a sibling doesn't → each still-owning arm ends with
  `drop name` (add `else { drop name }` to an else-less `if`).
- `return`, `break`, `continue` → drop every still-owned local (except a
  returned value) immediately before the jump.
- No drop for: reassignment (`x = expr` consumes the old value itself),
  discarded call results in statement position, unbound match payloads, or
  the `?` early-return path.

## Builtins

```text
print(x)                      # reads x — debug-print
print_str(s)                  # reads s — print without quotes
vec() -> Vec<T>               # empty vector (context infers T)
push(v, x) -> Vec<T>          # consumes v, returns it with x appended
len(v) -> Int                 # reads v
get(v, i) -> Option<T>        # reads v — element copy at i
range(a, b) -> Vec<Int>       # integers a..b-1
sort(v) -> Vec<T>             # consumes v, returns it sorted
min(v) -> Option<T>           # reads v — smallest element, or None if empty
max(v) -> Option<T>           # reads v — largest element, or None if empty
sum(v) -> Int                 # reads v — sum of an Int vec, 0 if empty
contains(v, x) -> Bool        # reads v and x — true if v has x
count(v, x) -> Int            # reads v and x — occurrences of x in v
count_if(v, x -> b) -> Int    # reads v — how many satisfy the predicate
reverse(v) -> Vec<T>          # consumes v, returns it reversed
swap(v, i, j) -> Vec<T>       # consumes v — exchanges positions i and j
set(v, i, x) -> Vec<T>        # consumes v and x — replaces element i
unwrap_or(o, d) -> T          # consumes both — Some(x) -> x, None -> d
clone(x) -> T                 # reads x — fresh copy
str_len(s) -> Int             # reads s
concat(a, b) -> Str           # consumes both, returns a+b
chars(s) -> Vec<Str>          # reads s — one-char strings
int_to_str(n) -> Str
parse_int(s) -> Option<Int>   # reads s
to_float(n) -> Float
trunc(x) -> Int               # toward zero
```

A `fn` you define with a builtin's name shadows it for the whole program —
the builtin, method form included, becomes unreachable there.

## Example

```
fn sum_big(v: &Vec<Int>, limit: Int) -> Int {
    let total = 0
    for x in &v {
        if x > limit {
            total = total + x
        }
    }
    total
}

fn extend(v: Vec<Int>, x: Int) -> Vec<Int> {
    push(v, x)
}

fn main() {
    let nums = push(push(vec(), 5), 40)
    let bigger = extend(clone(&nums), 70)
    print(sum_big(&nums, 10))
    drop nums
    print(sum_big(&bigger, 10))
    drop bigger
}
```

`sum_big` only reads `v` (declared `&Vec<Int>`, used `&v`); `extend` consumes
it (bare). Each `drop` follows its variable's last-read statement.

## Reading compiler errors

`OX03xx` are type errors and `OX04xx` ownership errors in the underlying
program. `EX00xx` mean your annotations disagree with the real data flow:
`EX0001` — `&` on a consuming use (remove it); `EX0002` — bare read of a
linear value (write `&name`); `EX0003` — missing `drop` at the value's
last-use point; `EX0004` — a `drop` where the value is not owned or not
dead; `EX0005` — wrong parameter mode (read-only is `name: &Type`, consumed
is `name: Type`).
